buildscript {
    ext {
        buildToolsVersion = "28.0.3"
        minSdkVersion = 19
        compileSdkVersion = 28
        targetSdkVersion = 28
        supportLibVersion = "28.0.0"
        reactNavigationRNVersion = "reactNative57"
    }
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0'
    }
}

allprojects {
    repositories {
        google()
        mavenLocal()
        jcenter()
        maven {
            url "$rootDir/../node_modules/react-native/android"
        }
        maven {
            url 'https://jitpack.io'
        }
    }
}

subprojects { subproject ->
    afterEvaluate {project ->
        // Override some compilation values for Android dependencies
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion rootProject.ext.compileSdkVersion
                buildToolsVersion rootProject.ext.buildToolsVersion
            }
        }

        // Ignore React Native Navigation flavors other than the target flavor
        if (subproject.name == 'react-native-navigation') {
            android {
                variantFilter { variant ->
                    def names = variant.flavors*.name
                    if (!names.contains(rootProject.ext.reactNavigationRNVersion)) {
                        setIgnore(true)
                    }
                }
            }
        }
    }
}
